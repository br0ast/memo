# Refactored Formal Competency Questions (Iteration 4)

## CQ_4.1
Return all the authors of second-level glosses which annotate or cite `manuscript_1`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX litre: <http://www.essepuntato.it/2010/06/literalreification/>
PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?glossator ?name
WHERE {
  ?gloss a memo:Gloss ;
           (memo:annotates|cito:cites) / (memo:annotates|cito:cites) :manuscript_1 ;
           dcterms:creator ?glossator .
  ?glossator litre:hasLiteral ?literalname .
  ?literalname litre:hasLiteralValue ?name .
  FILTER EXISTS
         {
           ?gloss dcterms:created ?date .
           ?literalname dcterms:valid ?interval .
           ?interval ti:hasIntervalStartDate ?start ;
                     ti:hasIntervalEndDate ?end .
            FILTER 
                   (
                     ?date >= ?start && 
                     ?date <= ?end 
                   )
  		  }
}
```
### Result
* `glossator_4`, "Guriffis";

## CQ_4.2
Return all the name variants of `author_2` under which he created a manuscript, all the manuscripts he created and all the respective glossators of each manuscript.

```
PREFIX : <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX litre: <http://www.essepuntato.it/2010/06/literalreification/>
PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>

SELECT DISTINCT ?name ?manuscript ?glossator
WHERE
{
    :author_2 ^dcterms:creator ?manuscript ;
              litre:hasLiteral ?literal .
    ?gloss a memo:Gloss ;
             ^frbr:part ?manuscript ;
             dcterms:creator ?glossator .
    ?literal litre:hasLiteralValue ?name .
    ?manuscript dcterms:created ?date .
    FILTER EXISTS
           {
             ?literal dcterms:valid ?interval .
             ?interval ti:hasIntervalStartDate ?start ;
                       ti:hasIntervalEndDate ?end .
             FILTER 
             (
               ?date >= ?start && 
               ?date <= ?end
             )
           }  
}
```
### Result
* "Serpico", `manuscript_2`, `glossator_3`
* "Serpico", `manuscript_2`, `glossator_4`
* "Serpico", `manuscript_3`, `glossator_5` 
