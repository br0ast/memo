# Refactored Formal Competency Questions (Iteration 4)

## CQ_4.1
Return all the authors of second-level glosses which annotate or cite `ex:manuscript_1`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX literal: <http://www.essepuntato.it/2010/06/literalreification/>
PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?glossator ?name
WHERE {
  ?gloss a memo:Gloss ;
           (memo:annotates|cito:cites) / (memo:annotates|cito:cites) ex:manuscript_1 ;
           dcterms:creator ?glossator .
  ?glossator literal:hasLiteral ?literalname .
  ?literalname literal:hasLiteralValue ?name .
  FILTER EXISTS
         {
           ?gloss dcterms:created ?date .
           ?literalname dcterms:valid ?interval .
           ?interval ti:hasIntervalStartDate ?start ;
                     ti:hasIntervalEndDate ?end .
            FILTER 
                   (
                     ?date >= ?start && 
                     ?date <= ?end 
                   )
  		  }
}
```
### Result
* `ex:glossator_4`, "Guriffis";

## CQ_4.2
Return all the name variants of `ex:author_2` under which he created a manuscript, all the manuscripts he created and all the respective glossators of each manuscript.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX literal: <http://www.essepuntato.it/2010/06/literalreification/>
PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>

SELECT DISTINCT ?name ?manuscript ?glossator
WHERE
{
    ex:author_2 ^dcterms:creator ?manuscript ;
              literal:hasLiteral ?literal .
    ?gloss a memo:Gloss ;
             ^frbr:part ?manuscript ;
             dcterms:creator ?glossator .
    ?literal literal:hasLiteralValue ?name .
    ?manuscript dcterms:created ?date .
    FILTER EXISTS
           {
             ?literal dcterms:valid ?interval .
             ?interval ti:hasIntervalStartDate ?start ;
                       ti:hasIntervalEndDate ?end .
             FILTER 
             (
               ?date >= ?start && 
               ?date <= ?end
             )
           }  
}
```
### Result
* "Serpico", `ex:manuscript_2`, `ex:glossator_3`
* "Serpico", `ex:manuscript_2`, `ex:glossator_4`
* "Serpico", `ex:manuscript_3`, `ex:glossator_5` 
