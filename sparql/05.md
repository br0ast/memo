# Refactored Formal Competency Questions (Iteration 5)

## CQ_5.1
Return all the glosses that refer to the text embodied in `ex:folio_2` but that are not themselves embodied in it.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss ;
         frbr:embodiment ?folio ;
         dcterms:relation ?text .
  ?text a memo:Text ;
        frbr:embodiment ex:folio_2 .
  ?folio a memo:Folio .
  FILTER ( ex:folio_2 != ?folio )
}
```
### Result
* `ex:gloss_b`
* `ex:gloss_g`

## CQ_5.2
Return all the folios embodying second-level glosses, which have been made by `ex:glossator_2` and refer to or cite or annotate `ex:text_1`, and also return the aforementioned glosses.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?folio ?gloss
WHERE {
  ?gloss a memo:Gloss ;
         frbr:embodiment ?folio ;
         dcterms:creator ex:glossator_2 ;
         (dcterms:relation|cito:cites|memo:annotates) / (dcterms:relation|cito:cites|memo:annotates) ex:text_1 .
}
```
### Result
* `ex:folio_2`, `ex:gloss_d`
* `ex:folio_2`, `ex:gloss_e`

## CQ_5.3
Return all the folios in which more than one manuscript text is embodied.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT ?folio (COUNT(?folio) AS ?count)
WHERE {
  ?manuscript frbr:part ?text .
  ?text a memo:Text ;
        frbr:embodiment ?folio .
}
GROUP BY ?folio HAVING ( ?count > "1"^^xsd:integer )
```
### Result
* `ex:folio_7`
