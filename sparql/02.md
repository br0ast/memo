# Refactored Formal Competency Questions (Iteration 2)

## CQ_2.1
Return all the textual metadata in time with textual role "incipit" which have been provided between 1750 and 1850 and the manuscripts they are related to.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
PREFIX tvc: <http://purl.org/spar/tvc/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?tmit ?manuscript
WHERE {
  ?tmit a memo:TextualMetadataInTime ;
		    ^memo:hasTextualMetadata ?manuscript ;
        memo:withTextualRole memo:incipit ;
        tvc:atTime ?time .
  ?time ti:hasIntervalStartDate ?date .
  FILTER
  (
    ?date > "1750"^^xsd:gYear && 
    ?date < "1850"^^xsd:gYear
  )
}
```
### Result
* `ex:incipit_1`, `ex:manuscript_1`
* `ex:incipit_3`, `ex:manuscript_3`

## CQ_2.2
Return all the textual metadata in time whose contents are based only on a text, their respective textual roles and the manuscripts they are related to.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?tmit ?role ?manuscript
WHERE {
  ?tmit a memo:TextualMetadataInTime ;
		    ^memo:hasTextualMetadata ?manuscript ;
        memo:withTextualRole ?role ;
        memo:relatesToTextualContext ?context .
  FILTER NOT EXISTS
  {
    ?text a memo:Gloss .
    ?context memo:isBasedOn ?text .
  }
}
```
### Result
* `ex:f_rubric_1`, `memo:finalRubric`, `ex:manuscript_1`
* `ex:incipit_2`, `memo:incipit`, `ex:manuscript_2`
* `ex:explicit_2`, `memo:explicit`, `ex:manuscript_2`
* `ex:incipit_3`, `memo:incipit`, `ex:manuscript_3`

## CQ_2.3
Return all the textual metadata in time which have been created by `ex:agent_1` starting from 1780, their textual roles, their dates and the manuscripts they are related to.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ti: <http://www.ontologydesignpatterns.org/cp/owl/timeinterval.owl#>
PREFIX tvc: <http://purl.org/spar/tvc/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?tmit ?role ?date ?manuscript
WHERE {
  ?tmit a memo:TextualMetadataInTime ;
		    ^memo:hasTextualMetadata ?manuscript ;
        memo:withTextualRole ?role ;
        dcterms:creator ex:agent_1 ;
        tvc:atTime ?time .
  ?time ti:hasIntervalStartDate ?date .
  FILTER ( ?date >= "1780"^^xsd:gYear )
}
```
### Result
* `ex:explicit_1`, `memo:explicit`, "1780", `ex:manuscript_1`
* `ex:f_rubric_1`, `memo:finalRubric`, "1780", `ex:manuscript_1`
* `ex:incipit_3`, `memo:incipit`, "1800", `ex:manuscript_3`
