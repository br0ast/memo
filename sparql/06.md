# Refactored Formal Competency Questions (Iteration 6)

## CQ_6.1
Return all the codices in which at least one folio is made of a different material with respect to the other folios.

```
PREFIX : <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT DISTINCT ?codex
WHERE {
    ?codex a memo:Codex ;
           frbr:part ?folio ;
           frbr:part ?folio2 .
    ?folio a memo:Folio ;
           dcterms:medium ?a .
    ?folio2 a memo:Folio ;
           dcterms:medium ?b .
  FILTER (?a != ?b )
}
```
### Result
* `codex_1`

## CQ_6.2
Return all the codices whose binding size measurement (measured in millimeters) is different with respect to the folios they are made up by.

```
PREFIX : <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT DISTINCT ?codex
WHERE {
    ?codex a memo:Codex ;
           frbr:part ?folio , ?binding .
    ?binding a memo:Binding ;
             dcterms:extent ?extent .
    ?extent a memo:SizeMeasurement ;
            memo:hasSizeMeasurementUnit memo:millimeters ;
            memo:hasWidth ?width ;
            memo:hasLength ?height .
    ?folio a memo:Folio ;
           dcterms:extent ?extent2 .
    ?extent2 a memo:SizeMeasurement ;
             memo:hasSizeMeasurementUnit memo:millimeters ;
             memo:hasWidth ?width2 ;
             memo:hasLength ?height2 .
  FILTER ( ?width != ?width2 && ?height != ?height2 )
}
```
### Result
* `codex_2`

## CQ_6.3
Return all the codices in which at least one side of a folio has a number of columns that is different with respect the sides of the other folios.

```
PREFIX : <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT DISTINCT ?codex
WHERE {
  ?codex a memo:Codex ;
         frbr:part ?folio ;
         frbr:part ?folio2 .
  ?folio a memo:Folio ;
         frbr:part ?side .
  ?side memo:hasNumberOfColumns ?n .
  ?folio2 a memo:Folio ;
          frbr:part ?side2 .
  ?side2 memo:hasNumberOfColumns ?m .
  FILTER ( ?n != ?m )
}
```
### Result
* `codex_1`

## CQ_6.4
Return all the titles of the manuscripts contained in codices being cited by works created by `author_I`, the identifier of each codex and the title of each work.
```
PREFIX : <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX cito: <http://purl.org/spar/cito#>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX fabio: <http://purl.org/spar/fabio#>

SELECT DISTINCT ?manuscripttitle ?id ?booktitle
WHERE {
  ?manuscript a memo:Manuscript ;
              dcterms:title ?manuscripttitle ;
              frbr:part ?text .
  ?text a memo:Text ;
        frbr:embodiment ?folio .
  ?folio a memo:Folio ;
         ^frbr:part ?codex .
  ?codex a memo:Codex ;
         dcterms:identifier ?id ;
         ^cito:cites ?book .
  ?book a fabio:Book ;
        dcterms:creator :author_I ;
        dcterms:title ?booktitle.
}
```
### Result
* "Arbor cum Glossis Ioannis Phasellus", "001", "Vermischte Schriften"
* "Epistula ad Barbalum", "001", "Vermischte Schriften"
* "Fragmenta Decreti", "002", "Vermischte Schriften"