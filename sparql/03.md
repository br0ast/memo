# Refactored Formal Competency Questions (Iteration 3)

## CQ_3.1
Return the entities cited by the textual metadata in time of `ex:manuscript_2`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?entity

WHERE {
  ?tmit ^memo:hasTextualMetadata ex:manuscript_2 ;
        cito:cites ?entity .
}
```
### Result
* `ex:manuscript_1`
* `ex:gloss_g`

## CQ_3.2
Return all the titles of the critical editions cited by second-level glosses that cite or annotate `ex:text_2`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT ?title
WHERE {
  ?gloss a memo:Gloss ;
         (cito:cites|memo:annotates) / (cito:cites|memo:annotates) ex:text_2 ;
         cito:cites ?edition .
  ?edition a memo:CriticalEditionVolume ;
           dcterms:title ?title
}
```
### Result
* "Edition One"
* "Edition Two"

## CQ_3.3
Return all the manuscripts in which at least a second-level gloss that annotates a text cites the same critical edition.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/>
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT ?manuscript
WHERE {
  ?text a memo:Text .
  ?gloss a memo:Gloss ;
           ^frbr:part ?manuscript ;
           memo:annotates / memo:annotates ?text ;
           cito:cites ?edition .
  ?edition a memo:CriticalEditionVolume .
}
```
### Result
* `ex:manuscript_2`
* `ex:manuscript_3`
