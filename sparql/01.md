# Refactored Formal Competency Questions (Iteration 1)

## CQ_1.1
Return all the glosses that are part of `manuscript_1`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part :manuscript_1
}
```
### Result
* `gloss_a`
* `gloss_b`
* `gloss_c`
* `gloss_d`
* `gloss_e`
* `gloss_f`

## CQ_1.2
Return all the first-level glosses that are part of `manuscript_1` and cite `text_1`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX cito: <http://purl.org/spar/cito#>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part :manuscript_1 .
  ?gloss cito:cites :text_1
}
```
### Result
* `gloss_a`
* `gloss_f`

## CQ_1.3
Return all the second-level glosses that are part of `manuscript_1` and that annotate or cite `text_1`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX cito: <http://purl.org/spar/cito#>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part :manuscript_1 .
  ?gloss (memo:annotates|cito:cites) / (memo:annotates|cito:cites) :text_1
}
```
### Result
* `gloss_c`

## CQ_1.4
Return all the second-level glosses that are part of `manuscript_1` and that annotate or refer to `gloss_a`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX cito: <http://purl.org/spar/cito#>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part :manuscript_1 .
  ?gloss (memo:annotates|dcterms:relation) / (memo:annotates|dcterms:relation) :gloss_a
}
```
### Result
* `gloss_e`
* `gloss_f`

## CQ_1.5
Return all the second-level glosses that annotate or cite `text_1`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito#>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss (memo:annotates|cito:cites) / (memo:annotates|cito:cites) :text_1
}
```
### Result
* `gloss_c`
* `gloss_h`

## CQ_1.6
Return all the third-level glosses that annotate `gloss_b`.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss memo:annotates / memo:annotates / memo:annotates :gloss_b
}
```
### Result
* `gloss_h`

## CQ_1.7
Return all the glosses that cite a manuscript.

```
PREFIX : <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito#>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss ;
         cito:cites ?manuscript .
  ?manuscript a memo:Manuscript .
}
```
### Result
* `gloss_g`