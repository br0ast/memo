# Refactored Formal Competency Questions (Iteration 1)

## CQ_1.1
Return all the glosses that are part of `ex:manuscript_1`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part ex:manuscript_1
}
```
### Result
* `ex:gloss_a`
* `ex:gloss_b`
* `ex:gloss_c`
* `ex:gloss_d`
* `ex:gloss_e`
* `ex:gloss_f`

## CQ_1.2
Return all the first-level glosses that are part of `ex:manuscript_1` and cite `ex:text_1`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part ex:manuscript_1 .
  ?gloss cito:cites ex:text_1
}
```
### Result
* `ex:gloss_a`
* `ex:gloss_f`

## CQ_1.3
Return all the second-level glosses that are part of `ex:manuscript_1` and that annotate or cite `ex:text_1`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part ex:manuscript_1 .
  ?gloss (memo:annotates|cito:cites) / (memo:annotates|cito:cites) ex:text_1
}
```
### Result
* `ex:gloss_c`

## CQ_1.4
Return all the second-level glosses that are part of `ex:manuscript_1` and that annotate or refer to `ex:gloss_a`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX frbr: <http://purl.org/vocab/frbr/core#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX dcterms: <http://purl.org/dc/terms/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss ^frbr:part ex:manuscript_1 .
  ?gloss (memo:annotates|dcterms:relation) / (memo:annotates|dcterms:relation) ex:gloss_a
}
```
### Result
* `ex:gloss_e`
* `ex:gloss_f`

## CQ_1.5
Return all the second-level glosses that annotate or cite `ex:text_1`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss (memo:annotates|cito:cites) / (memo:annotates|cito:cites) ex:text_1
}
```
### Result
* `ex:gloss_c`
* `ex:gloss_h`

## CQ_1.6
Return all the third-level glosses that annotate `ex:gloss_b`.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss .
  ?gloss memo:annotates / memo:annotates / memo:annotates ex:gloss_b
}
```
### Result
* `ex:gloss_h`

## CQ_1.7
Return all the glosses that cite a manuscript.

```
PREFIX ex: <https://w3id.org/irnerio/data/memo/> 
PREFIX memo: <https://w3id.org/irnerio/ontology/memo/>
PREFIX cito: <http://purl.org/spar/cito/>

SELECT ?gloss
WHERE {
  ?gloss a memo:Gloss ;
         cito:cites ?manuscript .
  ?manuscript a memo:Manuscript .
}
```
### Result
* `ex:gloss_g`
